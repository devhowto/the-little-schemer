== 2. Do it Again and Again

`'()` is a `lat?` because it doesn't contain a list (even though it doesn't contain any atom either, but that is their definition).


===  lat?

[source,scheme]
----
;;;;
;; lat? :: List -> Bool
;;
;; Checks whether `l` is a list of atoms.
;;
(define lat?
  (lambda (l)
    (cond
     ((null? l) #t)
     ((atom? (car l)) (lat? (cdr l)))
     (else #f))))
----

- `(cond ...)` asks questions
- `(lambda ...)` creates a function; and
- `(define ...)` gives the function a name.

If the value of `(atom? (car l))` is `#t`, then the value of the entire function is `(lat? (cdr l))`.

`(lat? (cdr l))` asks if the rest of the list is composed only of atoms, invoking the function with a new argument.

===  member? v1

.member? function definition
[source,scheme]
----
;;;;
;; member? :: Atom List -> Bool
;;
;; Checks whether `a` is member of the `lat`.
;;
(define member?
  (lambda (a lat)
    (cond
     ((null? lat) #f)
     ((eq? a (car lat)) #t)
     (else (member? a (cdr lat))))))
----

===  member? v2

Makes use of `or` so we don't return `#t` explicitly like v1.

[source,scheme]
----
;;;;
;; member? :: Atom List -> Bool
;;
;; Checks whether `a` is member of the `lat`.
;;
(define member?
  (lambda (a lat)
    (cond
     ((null? lat) #f)
     (else (or (eq? (car lat) a)
               (member? a (cdr lat)))))))
----
