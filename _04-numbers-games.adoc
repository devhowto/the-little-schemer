== 4. Numbers Games

Numbers are atoms, but for this book we only consider non-negative numbers (Natural Set), and ignore negative and floating point numbers.

For this book `(sub1 0)` is a “no answer” thing, but it practice it would produce `-1`.


=== o+

.o+ function definition
[source,scheme]
----
;;;;
;; o+ :: Natural Natural -> Natural
;;
;; Computes the sum of `n` and `m`.
;;
(define o+
  (lambda (n m)
    (cond
     ((zero? m) n)
     (else (o+ (add1 n) (sub1 m))))))

(o+ 1 1)
----

We broke the first commandment as we did not use `null?`. Still asking `zero?` of a number is somewhat like asking `null?` of a list.

`zero?` is like `null?` and `add1` (builds numbers) is like `cons` (builds lists).


=== o-

.o- function definition
[source,scheme]
----
;;;;
;; o- :: Natural Natural -> Natural
;;
;; Computes the difference of `n` and `m` (subtracts `m` from `n`).
;;
(define o-
  (lambda (n m)
    (cond
     [(zero? m) n]
     [else (o- (sub1 n) (sub1 m))])))
----


A *tuple* is a list of numbers. `(3 (7 4) 13 9)` is not a tuple because it contains a list, that is, `(7 4)` is not a number.

`'()` is a tup. It is the empty tup.

The natural way to build numbers from list is to use `o+` instead of `cons`.

The value for the terminal condition for building lists with `cons` is `'()`. The value for terminal condition when building numbers with `o+` is `0`.

* terminal condition for a list: `(null? my-list)`.
** `+((null? my-list) '())+`
* terminal condition for a tup: `(null? my-tup)`.
** `((null? my-tup) 0)`, that is, `0` for adding/subtracting. `1` for multiplying (not sure (yet) about division).


A list is an empty list, or a list with an atom `(car my-list)` and `(cdr my-list)` is yet another list.

A tuple is an empty list, or a list with a number `(car my-tup)` and `(cdr my-tup)` which is yet another tup.

A number is either `zero` or it is one added to a “rest”, where rest is again a number.

The natural terminal condition for numbers is `(zero? n)`. The natural recursion on a number is `(sub1 n)` because we want to reach `0`.

=== addtup

.addtup function definition
[source,scheme]
----
;;;;
;; addtup :: [Natural] -> Natural
;;
;; Adds up all elements of `tup`.
;;
(define addtup
  (lambda (tup)
    (cond
     ((null? tup) 0)
     (else (o+ (car tup)
               (addtup (cdr tup)))))))
----

=== Note About Recursion

When we recur (not using tail-recursion), the recursive call is _never_ in the combination part. The combination part is always handled by some other operation, and the recursive invocation happens more at the end of the expression, generally involving `cdr` or `sub1`.
Just take a look at the `else` question of our procedures and see the similarities.


=== o*

.o* function definition
[source,scheme]
----
;;;;
;; o* :: Natural Natural -> Natural
;;
;; Calculates the product of `n` and `m`.
;;
(define o*
  (lambda (n m)
    (cond
     ((zero? m) 0)
     (else (o+ n (o* n (sub1 m)))))))
----

This is how the above definition would expand for `(o* 3 4)`:

.stepping through (o* 3 4) expansion
[source,scheme]
----
(o* 3 4)
(o+ 3 (o* 3 3))
(o+ 3 (o+ 3 (o* 3 2)))
(o+ 3 (o+ 3 (o+ 3 (o* 3 1))))
(o+ 3 (o+ 3 (o+ 3 (o+ 3 (o* 3 0)))))
(o+ 3 (o+ 3 (o+ 3 (o+ 3 0))))
(o+ 3 (o+ 3 (o+ 3 3)))
(o+ 3 (o+ 3 6))
(o+ 3 9)
12
----

Or:

[source,text]
----
12 * 3 = 12 + 12 * 2
       = 12 + 12 + 12 * 1
       = 12 + 12 + 12 + 12 * 0
       = 12 + 12 + 12 + 0
       = 12 + 12 + 12
       = 12 + 24
       = 36
----

=== tup+

.tup+ function definition
[source,scheme]
----
;;;;
;; tup+ :: [Natural] [Natural] -> [Natural]
;;
;; tup+ :: Adds pairs of numbers from `tup1` and `tup2`.
;;
;; If one tup is lengthier, appends those elements to the end
;; of the resulting list.
;;
(define tup+
  (lambda (tup1 tup2)
    (cond
     ((null? tup1) tup2)
     ((null? tup2) tup1)
     (else (cons (+ (car tup1) (car tup2))
                 (tup+ (cdr tup1) (cdr tup2)))))))
----

=== o>

.o> function definition
[source,scheme]
----
;;;;
;; o> :: Int Int -> Bool
;;
;; Checks whether `n` is greater than `m`.
;;
;; NOTE: The order of the two `(zero? ...)` conditions matter
;; when `n` and `m` are the same number.
;;
(define o>
  (lambda (n m)
    (cond
     ((zero? n) #f)
     ((zero? m) #t)
     (else (o> (sub1 n) (sub1 m))))))
----

1. The order of the questions matter. If we asked `(zero? m)` before `(zero? n)`, we would produce `#t` even for cases when both numbers are actually equal.


=== o<

.o< function definition
[source,scheme]
----
;;;;
;; o< :: Int Int -> Bool
;;
;; Checks whether `n` is less than `m`.
;;
;; NOTE: The order of the two `(zero? ...)` conditions matter
;; when `n` and `m` are the same number. We MUST check for
;; `(zero? m)` first.
;;
(define o<
  (lambda (n m)
    (cond
     ((zero? m) #f)
     ((zero? n) #t)
     (else (o< (sub1 n) (sub1 m))))))
----

=== o=

.o= function definition
[source,scheme]
----
;;;;
;; o= :: Int Int -> Bool
;;
;; Checks whether `n` and `m` are the same value.
;;
(define o=
  (lambda (n m)
    (cond
     ((zero? n) (zero? m))
     ((zero? n) #f)
     (else (o= (sub1 n) (sub1 m))))))
----

.o= using o> and o<
[source,scheme]
----
(define o=
  (lambda (n m)
    (cond
     ((o> n m) #f)
     ((o< n m) #f)
     (else #t))))
----

=== o**

.o** function definition
[source,scheme,lineos]
----
;;;;
;; o** :: Natural Natural -> Natural
;;
;; Raises `b` to the `e`th power.
;;
(define o**
  (lambda (b e)
    (cond
     ((zero? e) 1)
     (else (o* b (o** b (sub1 e)))))))
----

The above implementation expands to something more or less like this:

[source,scheme]
----
(o** 2 3)
(o* 2 (o** 2 2))
(o* 2 (o* 2 (o* 2 1)))
(o* 2 (o* 2     2))
(o* 2     4)
----

=== o/

.o/ function definition
[source,scheme]
----
;;;;
;; o/ :: Int Int -> Int
;;
;; Computes the integer division for `n` and `m`, that is,
;; how many times `m` goes into the first`.
;;
;; NOTE: The remainder is ignored.
;;
(define o/
  (lambda (n m)
    (cond
     ((< n m) 0)
     (else (add1 (o/ (- n m) m))))))
----

The above expands to something like this:

[source,scheme]
----
(o/ 9 2)
(add1 (o/ 7 2))
(add1 (add1 (o/ 5 2)))
(add1 (add1 (add1 (o/ 3 2))))
(add1 (add1 (add1 (add1 (o/ 1 2)))))
(add1 (add1 (add1 (add1 0))))
(add1 (add1 (add1 1)))
(add1 (add1 2))
(add1 3)
4
----



=== len

.len function definition
[source,scheme,lineos]
----
;;;;
;; len :: [Atom] -> Int
;;
;; Computes the length of `lat`.
;;
(define len
  (lambda (lat)
    (cond
     ((null? lat) 0)
     (else (add1 (len (cdr lat)))))))
----


=== pick

.pick function definition
[source,scheme,lineos]
----
;;;;
;; pick :: Int [Atom] -> Atom
;;
;; Retrieves the atom at index `n`.
;;
;; NOTE: `pick` is defined only for non-empty lists and for
;; non-zero indexes.
;;
;; NOTE: If not found, returns the last atom blindly; that
;; is, assume the atom is present.
;;
(define pick
  (lambda (n lat)
    (cond
     ((zero? (sub1 n)) (car lat))
     (else (pick (sub1 n) (cdr lat))))))
----

In `pick`, `(car lat)` is always in sync with `n`.


=== rempick

.rempick function definition
[source,scheme]
----

;;;;
;; rempick :: Int [Atom] -> [Atom]
;;
;; Returns the `lat` with element at `n` removed.
;;
;; NOTE: `rempick` is defined only for non-empty lists and for
;; non-zero indexes.
;;
;; ASSUME: The element at index `n` is a member of the `lat`.
;;
(define rempick
  (lambda (n lat)
    (cond
     ((zero? (sub1 n)) (cdr lat))
     (else (cons
            (car lat)
            (rempick (sub1 n) (cdr lat)))))))
----

=== About PRIMITIVES


Can you write `number?` which is true if its argument is a numeric atom and false if it is anything else?

No: `number?`, like `add1`, `sub1`, `zero?`, `car`, `cdr`, `cons`, `null?`, `eq?`, and `atom?`, is a primitive function.


link:https://groups.google.com/forum/#!topic/comp.lang.scheme/1owCrv0H3lk[My question on comp.lang.scheme about primitives.]


=== no-nums

.no-nums function definition
[source,scheme,lineos]
----
;;;;
;; no-nums :: [Atom] -> [Atom]
;;
;; Returns `lat` with all numbers removed.
;;
(define no-nums
  (lambda (lat)
    (cond
     ((null? lat) '())
     ((number? (car lat)) (no-nums (cdr lat)))
     (else (cons (car lat)
                 (no-nums (cdr lat)))))))
----

=== `all-nums'

.`all-nums' function definition
[source,scheme,lineos]
----
include::./definitions.scm[tag=all-nums]
----

Numbers are atoms. Still, we use `=` for numbers and `eq?' for all other atoms.


=== `eqan?'

.`eqan?' function definition
[source,scheme,lineos]
----
include::./definitions.scm[tag=eqan?]
----

=== `occur'

.`occur' function definition
[source,scheme,lineos]
----
include::./definitions.scm[tag=occur]
----


=== `one?'

.`one?' function definition
[source,scheme,lineos]
----
include::./definitions.scm[tag=one?]
----


=== `rempick-v2'

.`rempick-v2' function definition
[source,scheme,lineos]
----
include::./definitions.scm[tag=rempick-v2]
----


